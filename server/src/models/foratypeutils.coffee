ForaTypeUtilsBase = require('./foratypeutils-base').ForaTypeUtilsBase
conf = require('../conf')
fs = require 'fs'
path = require 'path'
thunkify = require 'thunkify'
readdir = thunkify fs.readdir
stat = thunkify fs.stat
readfile = thunkify fs.readFile


class ForaTypeUtils extends ForaTypeUtilsBase


    getTrustedUserTypes: =>*
        definitions = {}
        
        Forum = require('./forum').Forum
        yield @addTrustedUserTypes Forum, 'forum', 'forums', definitions

        Post = require('./post').Post
        yield @addTrustedUserTypes Post, 'post', 'posts', definitions

        return definitions



    addTrustedUserTypes: (ctor, baseTypeName, dir, definitions) =>*
        extensionsDir = require("path").resolve(__dirname, '../extensions')

        typeDef = if typeof ctor.typeDefinition is "function" then ctor.typeDefinition() else ctor.typeDefinition
        
        for typeName in yield @getUserTypeDirectories path.join extensionsDir, dir
            for version in yield @getUserTypeDirectories path.join extensionsDir, dir, typeName
                ext = require(path.join extensionsDir, dir, typeName, version, 'model')
                def = JSON.parse JSON.stringify ext.typeDefinition
                definitions["#{dir}/#{typeName}/#{version}"] ?= def

                if typeDef.initialize
                    def.initialize = typeDef.initialize
                def.type = baseTypeName
                def.name = "#{dir}/#{typeName}/#{version}"
                def.version = version
                def.extensionType = 'builtin'

                for field in ['collection', 'trackChanges', 'autoGenerated', 'logging']
                    def[field] = typeDef[field]

                def.ctor = ctor

                def.inheritedProperties = []
                for k, v of typeDef.schema.properties
                    def.schema.properties[k] = v
                    def.inheritedProperties.push k

                for req in typeDef.schema.required
                    if def.schema.required?.indexOf(req) is -1
                        def.schema.required.push req
                        
        return



    getUserTypeDirectories: (dir) =>*
        dirs = []
        files = yield readdir dir
        for file, index in files
            filePath = "#{dir}/#{file}"
            entry = yield stat filePath
            if entry.isDirectory()
                dirs.push(file)
        dirs      

        
        
        
module.exports = ForaTypeUtils
